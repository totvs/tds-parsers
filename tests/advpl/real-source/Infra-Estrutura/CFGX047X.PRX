/*


Ŀ
Funo    x047Direto Autor  Gilson Nascimento      Data  26.02.97 
Ĵ
Descrio Monta Array com os arquivos do Diretorio no MV_DIRLOG       
Ĵ
Sintaxe                                                               
Ĵ
Parametros Sem Argumentos                                             
Ĵ
 Uso       CFGX047                                                    
ٱ


*/
#include "CFGX047x.ch"
Function x047Diretorio()
	LOCAL aDir := {},lRet,cArqLog

	cArqLog := ALLTRIM(GETMV("MV_DIRLOG"))
	cArqLog +=  IIF(Substr(cArqLog,Len(cArqLog),1) != "\","\","")
	aDir    := Directory(cArqLog+"*.LOG")
	lRet :=IIF(Len(aDir)==0,.T.,.F.)
	AEVAL(aDir , {|xArq| IIF(Ascan(aArq,DTOC(xArq[3])) == 0,AADD(aArq,DTOC(xArq[3])),nil) } )
	Return lRet

	/*/
	
	
	Ŀ
	Funo    x047Usuari Autor  Gilson Nascimento      Data  26.02.97 
	Ĵ
	Descrio Monta Array com os usuarios do sistema                      
	Ĵ
	Sintaxe                                                               
	Ĵ
	Parametros Sem Argumentos                                             
	Ĵ
	 Uso       CFGX047                                                    
	ٱ
	
	
	*/
Function x047Usuario()
	LOCAL c1,a1:={},a2     // Variaveis desnecessarias, somente p/ parametros.
	#IFDEF WINDOWS
		ReadUsuario(@c1,@a1,@a2)
	#ELSE
		x047ReadUsuario(@c1,@a1,@a2)
	#ENDIF
	AEVAL(a1,{|z|AADD(aUsu,z)})
	Return

	/*/
	
	
	Ŀ
	Funo    x047Empres Autor  Gilson Nascimento      Data  26.02.97 
	Ĵ
	Descrio Monta Array com as empresas do sistema                      
	Ĵ
	Sintaxe                                                               
	Ĵ
	Parametros Sem Argumentos                                             
	Ĵ
	 Uso       CFGX047                                                    
	ٱ
	
	
	*/
Function x047Empresa()
	LOCAL cAliasOld:=Alias(),nRegOld
	Local nSizeFil := 2

	//-- Atualiza o contedo da filial
	If FindFunction("FWSizeFilial")
		nSizeFil := FWSizeFilial()
	EndIf

	dbSelectArea("SM0")
	nRegOld := Recno()
	dbEval({||AADD(aEmpresas,{M0_CODIGO+Pad(M0_CODFIL,nSizeFil),Capital(Alltrim(M0_NOME))+" - "+Capital(Alltrim(M0_FILIAL))+Space(200)})})
	AEval(aEmpresas,{|x|AADD(aEmp,x[2]+x[1])})
	dbGoTo(nRegOld)
	dbSelectArea(cAliasOld)
	Return

	/*/
	
	
	Ŀ
	Funo    x047Modulo Autor  Gilson Nascimento      Data  26.02.97 
	Ĵ
	Descrio Monta Array com os modulos do SIGA                          
	Ĵ
	Sintaxe                                                               
	Ĵ
	Parametros Sem Argumentos                                             
	Ĵ
	 Uso       CFGX047                                                    
	ٱ
	
	
	*/
Function x047Modulo()
	Local aRetModName := RetModName()
	Local nI
	Local cMod

	For nI := 1 To Len(aRetModName)
		cMod := Subs(aRetModName[nI][2],5,3)
		If __lPyme .And. cMod <> "ESP" .And. ModIsPyme(cMod) .And. aRetModName[nI][4]
			Aadd(aModulos,{cMod,aRetModName[nI][3]})
		ElseIf !__lPyme .And. cMod <> "ESP" .And. aRetModName[nI][4]
			Aadd(aModulos,{cMod,aRetModName[nI][3]})
		EndIf
	Next nI

	AEval(aModulos,{|x|AADD(aMod,x[2])})
	Return

	/*/
	
	
	Ŀ
	Funo    x047Arquiv Autor  Gilson Nascimento      Data  26.02.97 
	Ĵ
	Descrio Monta Array com os arquivos do SX2 (Arquivos  Siga)         
	Ĵ
	Sintaxe                                                               
	Ĵ
	Parametros Sem Argumentos                                             
	Ĵ
	 Uso       CFGX047                                                    
	ٱ
	
	
	*/
Function x047Arquivo()
	LOCAL cAliasOld:=Alias(),cFilter
	dbSelectArea("SX2")
	cFilter := DBFILTER()
	dbClearFilter()
	dbEval( {||AADD(aSX2Alias,{.T.,X2_CHAVE,alltrim(X2_NOME)})} )
	AADD(aSX2Alias,{.T.,"SX1",oemtoansi(STR0020)})
	AADD(aSX2Alias,{.T.,"SX2",oemtoansi(STR0021)})
	AADD(aSX2Alias,{.T.,"SX3",oemtoansi(STR0022)})
	AADD(aSX2Alias,{.T.,"SX4",oemtoansi(STR0023)})
	AADD(aSX2Alias,{.T.,"SX5",oemtoansi(STR0024)})
	AADD(aSX2Alias,{.T.,"SX6",oemtoansi(STR0025)})
	AADD(aSX2Alias,{.T.,"SX7",oemtoansi(STR0026)})
	AADD(aSX2Alias,{.T.,"SX8",oemtoansi(STR0027)})
	AADD(aSX2Alias,{.T.,"SXB",oemtoansi(STR0028)})
	AADD(aSX2Alias,{.T.,"SXD",oemtoansi(STR0029)})
	AADD(aSX2Alias,{.T.,"   ",oemtoansi(STR0030)})
	//AEval(aSX2Alias,{|x|AADD(aSX2,{.T.,x[2]})})
	If !EMPTY(cFilter)
		dbSetFilter({||&cFilter},cFilter)
	Endif
	dbSelectArea(cAliasOld)
	Return

	/*/
	
	
	Ŀ
	Funo    x047TRB    Autor  Gilson Nascimento      Data  26.02.97 
	Ĵ
	Descrio Constroi o arquivo TRB para utilizacao no Browse            
	Ĵ
	Sintaxe                                                               
	Ĵ
	Parametros Sem Argumentos                                             
	Ĵ
	 Uso       CFGX047                                                    
	ٱ
	
	
	*/
Function x047TRB(cNomArq)
	LOCAL aCampos := {}

	/*--------------------- Codigos conforme o TIPO de Dado -------------------
	"S" - Start da Operacao
	"F" - Final da Operacao
	"I" - Registro ref. a operacao de inclusao
	"A" - Registro ref. a operacao de alteracao
	"R" - Registro ref. a impressao de relatorios
	"B" - Registro ref. a transacao Begin Transaction
	"E" - Registro ref. a transacao End Transaction
	"C" - Fechando o Siga Advanced
	"O" - Abrindo  o Siga Advanced
	"M" - Log de Email enviado
	"P" - Log de parametro confirmado
	-----------------------------------------------------------------------*/

	AADD(aCampos,{"FLAG"      ,"L",  1,0})
	AADD(aCampos,{"TIPO"      ,"C",  1,0})
	AADD(aCampos,{"RESUMO"    ,"M",10,0})
	AADD(aCampos,{"EMPRESA"   ,"C",  2,0})
	AADD(aCampos,{"FILIAL"    ,"C",  2,0})
	AADD(aCampos,{"MODULO"    ,"C", 10,0})
	AADD(aCampos,{"USUARIO"   ,"C", 15,0})
	AADD(aCampos,{"PROGRAMA"  ,"C",  8,0})
	AADD(aCampos,{"HORA"      ,"C",  8,0})
	AADD(aCampos,{"ARQUIVO"   ,"C",  3,0})
	AADD(aCampos,{"IDENT"     ,"C", 14,0})
	AADD(aCampos,{"REGISTRO"  ,"N", 10,0})
	AADD(aCampos,{"PORTA"     ,"C", 16,0})

	cNomArq := CriaTrab(aCampos)
	USE &cNomArq Alias TRB Exclusive NEW
	cNomInd1 := Subs(cNomArq,1,6)+"_1"
	cNomInd2 := Subs(cNomArq,1,6)+"_2"
	cNomInd3 := Subs(cNomArq,1,6)+"_3"
	dbCreateInd(cNomInd1,"IDENT+USUARIO+EMPRESA+FILIAL+HORA",{||IDENT+USUARIO+EMPRESA+FILIAL+HORA})
	dbCreateInd(cNomInd2,"EMPRESA+FILIAL+HORA+IDENT+USUARIO",{||EMPRESA+FILIAL+HORA+IDENT+USUARIO})
	dbCreateInd(cNomInd3,"MODULO+EMPRESA+FILIAL+HORA+IDENT+USUARIO",{||MODULO+EMPRESA+FILIAL+HORA+IDENT+USUARIO})
	dbClearIndex()
	dbSetIndex(cNomInd1)
	dbSetIndex(cNomInd2)
	dbSetIndex(cNomInd3)
	Return

	/*/
	
	
	Ŀ
	Funo    x047Browse Autor  Gilson Nascimento      Data  26.02.97 
	Ĵ
	Descrio Monta Array com os arquivos do Diretorio no MV_DIRLOG       
	Ĵ
	Sintaxe                                                               
	Ĵ
	Parametros Sem Argumentos                                             
	Ĵ
	 Uso       CFGX047                                                    
	ٱ
	
	
	*/
Function x047Browse(aFiltro,oBrw,cResumo,oResumo)
	LOCAL nBytes,cTexto:="",lAdicionar:=.F.
	LOCAL cFile
	LOCAL _cTipo,_cEmpresa,_cModulo,_cUsuario,_cPrograma,_cHora,_cRegistro,_cAlias,_cIdent,_cPorta
	Local nPos := 0
	Local nSizeFil := 2

	//-- Atualiza o contedo da filial
	If FindFunction("FWSizeFilial")
		nSizeFil := FWSizeFilial()
	EndIf

	xBuffer := Space(82)
	cFile :=ALLTRIM(GETMV("MV_DIRLOG"))
	cFile += IIF(Substr(cFile,Len(cFile),1) != "\","\","")
	cFile +=aFiltro[1]+".LOG"
	If File(cFile)
		FT_fuse(cfile)
		nBytes := FT_flastrec()
	Else
		#IFDEF WINDOWS
			MSGSTOP(oemtoansi(STR0031)+cFile,"Error")
		#ENDIF

		Return .F.
	Endif

	#IFNDEF WINDOWS
		ProcRegua(nBytes)
	#ELSE
		ProcRegua(nBytes)
	#ENDIF

	dbSelectArea("TRB")
	ZAP
	#IFDEF WINDOWS
		CursorWait()
	#ENDIF

	WHILE !ft_feof()
		lAdicionar:=.F.
		xbuffer := ft_fReadln()
		#IFNDEF WINDOWS
			IncProc()
		#ELSE
			IncProc()
		#ENDIF

		nPos    := 1
		_cTipo  := SUBSTR(xbuffer,nPos,1)

		nPos ++
		_cEmpresa := SUBSTR(xbuffer,nPos,nSizeFil+2)

		nPos += nSizeFil+2
		_cModulo := "SIGA"+SUBSTR(xbuffer,nPos,3)
		cModulo  := SUBSTR(xbuffer,nPos,3)

		nPos +=3
		_cUsuario := SUBSTR(xbuffer,nPos,15)

		nPos +=15
		_cPrograma := SUBSTR(xbuffer,nPos,8)

		nPos +=8
		_cHora := SUBSTR(xbuffer,nPos,2)+":"+SUBSTR(xbuffer,nPos+2,2)+":"+SUBSTR(xbuffer,nPos+4,2)

		nPos +=6
		_cRegistro := SUBSTR(xbuffer,nPos,10)

		nPos +=10
		_cAlias	:= SUBSTR(xbuffer,nPos,3)

		nPos +=3
		_cIdent := SUBSTR(xbuffer,nPos,14)

		nPos +=14
		_cPorta := SUBSTR(xbuffer,nPos,16)

		nPos +=16

		// Filtrar conforme a escolha do Usuario.
		lAdicionar := If(_cTipo $ aFiltro[6],.T.,.F.)
		lAdicionar := If(lAdicionar.and.IF(!Empty(aFiltro[2]),(Alltrim(_cUsuario)==Alltrim(aFiltro[2])),.T.),lAdicionar,.F.)
		lAdicionar := If(lAdicionar.and.IF(!Empty(aFiltro[3]),(_cEmpresa==aFiltro[3]),.T.),lAdicionar,.F.)
		lAdicionar := If(lAdicionar.and.IF(!Empty(aFiltro[4]),(Subs(_cModulo,5)==aFiltro[4]),.T.),lAdicionar,.F.)
		IF ("I"$aFiltro[6] .or. "A"$aFiltro[6])
			lAdicionar := If(lAdicionar.and._cAlias$aFiltro[5],lAdicionar,.F.)
		Endif
		If lAdicionar
			Do Case
			Case _cTipo=="O"
				cTexto := oemtoansi(STR0032)+_cHora+oemtoansi(STR0033)+_cModulo+"."
			Case _cTipo=="C"
				cTexto := oemtoansi(STR0033)+_cHora+oemtoansi(STR0033)+_cModulo+"."
			Case _cTipo=="S"
				cTexto := oemtoansi(STR0035)+_cPrograma+oemtoansi(STR0036)+_cHora+oemtoansi(STR0037)
			Case _cTipo=="F"
				cTexto := oemtoansi(STR0038)+_cPrograma+oemtoansi(STR0036)+_cHora+oemtoansi(STR0037)
			Case _cTipo=="B"
				cTexto := oemtoansi(STR0039)+_cHora+oemtoansi(STR0037)
			Case _cTipo=="E"
				cTexto := oemtoansi(STR0040)+_cHora+oemtoansi(STR0037)
			Case _cTipo=="I"
				cTexto := oemtoansi(STR0041)+_cRegistro+oemtoansi(STR0042)+_cAlias+oemtoansi(STR0036)+_cHora+oemtoansi(STR0037)
			Case _cTipo=="A"
				cTexto := oemtoansi(STR0043)+_cRegistro+oemtoansi(STR0042)+_cAlias+oemtoansi(STR0036)+_cHora+oemtoansi(STR0037)
			Case _cTipo=="R"
				cTexto := oemtoansi(STR0044)+_cPrograma+oemtoansi(STR0045)+_cRegistro+oemtoansi(STR0046)+_cHora+oemtoansi(STR0037)
				cTexto += If(Empty(_cPorta),oemtoansi(STR0047),oemtoansi(STR0048)+_cPorta)+"."
			Case _cTipo=="M"
				cTexto := Alltrim( Subs(xBuffer,nPos,255) )
			Case _cTipo=="P"
				cTexto := Alltrim( Subs(xBuffer,nPos) )
				cTexto := STRTRAN(ctexto,chr(254)+CHr(255),chr(13)+chr(10))
			Endcase
			dbAppend()
			REPLACE TIPO With _cTipo, RESUMO With cTexto, EMPRESA With Subs(_cEmpresa,1,2),;
				FILIAL With Subs(_cEmpresa,3,nSizeFil), MODULO With _cModulo, USUARIO With _cUsuario,;
				PROGRAMA With _cPrograma, REGISTRO With Val(_cRegistro),IDENT With _cIdent,;
				PORTA With _cPorta,HORA With _cHora,ARQUIVO With _cAlias
		Endif
		ft_fskip()
	END
	#IFDEF WINDOWS
		CursorArrow()
	#ENDIF
	FT_fuse()
	dbSetOrder(aFiltro[7])
	dbGoTop()
	cResumo:=TRB->RESUMO
	oResumo:Refresh()
	#IFDEF WINDOWS
		oBrw:Default()
		oBrw:Refresh()
	#ELSE
		x047MkBrow(aFiltro[1])
	#ENDIF
	Return .T.

	/*
	
	
	Ŀ
	Funo    x047Gerafi Autor  Gilson Nascimento      Data  26.02.97 
	Ĵ
	Descrio Monta Array com os dados utilizados para comparar o Arq. LOG
	          com os dados escolhidos pelos usuario.                      
	Ĵ
	Sintaxe                                                               
	Ĵ
	Parametros Sem Argumentos                                             
	Ĵ
	 Uso       CFGX047                                                    
	ٱ
	
	
	*/
Function x047GeraFiltro(cCombo1,cCombo2,cCombo3,cCombo4,lChk1,lChk2,lChk3,lChk4,lChk5,nRadio, lChk6,lChk7)
	LOCAL aRet:={},nc:=0,cString:=""
	Local lAmerican := ( GetSrvProfString("DATEFORMAT","") == "AMERICAN" )

	If lAmerican
		AADD(aRet,"SL"+Subs(cCombo1,4,2)+Subs(cCombo1,1,2)+If(Len(cCombo1)==8,Subs(cCombo1,7,2),Subs(cCombo1,9,2)))   // Nome do Arquivo LOG
	Else
		AADD(aRet,"SL"+Subs(cCombo1,1,2)+Subs(cCombo1,4,2)+If(Len(cCombo1)==8,Subs(cCombo1,7,2),Subs(cCombo1,9,2)))   // Nome do Arquivo LOG
	EndIf
	AADD(aRet,If(!(cCombo2$"Todos/All"),cCombo2,""))                                                      // Nome do Usuario ou Todos
	AADD(aRet,If(!(cCombo3$"Todas/All"),aEmpresas[ASCAN(aEmpresas,{|x|alltrim(x[1])==Substr(cCombo3,Len(cCombo3)-3,4)})][1],"")) // Cod. Empresa e Filial
	AADD(aRet,If(!(cCombo4$"Todos/All"),aModulos[ASCAN(aModulos,{|x|alltrim(cCombo4) $ alltrim(x[2])})][1],""))   // Cod. Modulo

	For nc:=1 to Len(aSX2Alias)
		cString += If(aSX2Alias[nc][1],aSX2Alias[nc][2],"")
	Next nc
	AADD(aRet,cString) // aSX2 Lista com os arquivos Escolhidos
	AADD(aRet,If(lChk1,"OCSF","")+if(lChk2,"I","")+If(lChk3,"A","")+If(lChk4,"RM","")+IF(lChk5,"BE","")+IF(lChk6,"M","")+IF(lChk7,"P","")) // Tipo de Transacao Escolhida
	AADD(aRet,nRadio)
	xFiltro1 := oemtoansi(STR0049)+cCombo2+oemtoansi(STR0050)+cCombo3+oemtoansi(STR0051)+cCombo4
	xFiltro2 := oemtoansi(STR0052)+If(lChk1,oemtoansi(STR0053),"")+If(lChk2,oemtoansi(STR0054),"")+If(lChk3,oemtoansi(STR0055),"")
	xFiltro2 += If(lChk4,oemtoansi(STR0056),"")+If(lChk5,oemtoansi(STR0057),"")
	xFiltro2 += oemtoansi(STR0058)+If(nRadio==1,oemtoansi(STR0059),If(nRadio==2,oemtoansi(STR0060),oemtoansi(STR0061)))

	Return aRet

	/*/
	
	
	Ŀ
	Funo    x047Relato Autor  Gilson Nascimento      Data  26.02.97 
	Ĵ
	Descrio Monta Array com os arquivos do Diretorio no MV_DIRLOG       
	Ĵ
	Sintaxe                                                               
	Ĵ
	Parametros Sem Argumentos                                             
	Ĵ
	 Uso       CFGX047                                                    
	ٱ
	
	
	*/
Function x047Relatorio(oDlg,cCombo1)
	LOCAL wnrel
	LOCAL cDesc1   := oemtoansi(STR0062)
	LOCAL cDesc2   := oemtoansi(STR0063)+cCombo1+".LOG"
	LOCAL cDesc3   := ""
	LOCAL cString  := "TRB"
	LOCAL aOrd     := {}

	PRIVATE Tamanho  := "M"
	PRIVATE titulo   := oemtoansi(STR0064)+cCombo1
	PRIVATE nomeprog := "CFGX047"
	PRIVATE aReturn := { oemtoansi(STR0065), 1,oemtoansi(STR0066), 1, 2, 1, "",1 }
	PRIVATE nLastKey := 0 ,cPerg := "CFG047", aInd := {}
	PRIVATE cArqRel := "SIGAEST.REL"

	//Ŀ
	// Verifica as perguntas selecionadas                           
	//
	//Ŀ
	// Verifica as perguntas selecionadas                           
	//mv_par01  - IMAGEM, RESUMO, AMBOS                             
	//
	pergunte(cPerg,.F.)

	wnrel:=SetPrint(cString,NomeProg,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,.F.,.F.,Tamanho,,.F.)

	If nLastKey == 27
		Set Filter To
		Return
	Endif

	SetDefault(aReturn,cString)

	If nLastKey = 27
		Set Filter To
		Return
	Endif

	#IFDEF WINDOWS
		RptStatus({|lEnd| X047Rel(@lEnd,wnRel,cString)},titulo)
	#ELSE
		X047Rel(.F.,wnRel,cString)
	#ENDIF
	dbGoTop()
	Return

	/*
	
	
	Ŀ
	Funo     x047Rel   Autor  Gilson Nascimento      Data  04/03/97 
	Ĵ
	Descrio  Chamada do Relatorio                                       
	Ĵ
	 Uso       x047Rel 			                                  
	ٱ
	
	
	*/
Function x047Rel(lEnd,WnRel,cString)
	LOCAL cabec1,cabec2
	LOCAL cRodaTxt := oemtoansi(STR0067)
	LOCAL nCntImpr := 0,nTipo := 0

	li := 80
	m_pag := 1
	//Ŀ
	// Inicializa os codigos de caracter Comprimido da impressora 
	//
	nTipo := 15

	//Ŀ
	// Monta os Cabecalhos                                          
	//
	titulo:= oemtoansi(STR0068)
	cabec1:= oemtoansi(STR0069)
	cabec2:=""
	/*
	0         1         2         3         4         5         6         7         8         9        10        11
	012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123
	TIPO EMPRESA MODULO     USUARIO         ROTINA   HORA     TABELA REGISTRO   PORTA
	X   99/99  XXXXXXXXXX XXXXXXXXXXXXXXX XXXXXXXX HH.MM.SS  XXX   XXXXXXXXXX XXXXXXXXXXXXXXXX
	xxxxxxxxxXxxxxxxxxXxxxxxxxxxXxxxxxxxxxXxxxxxxxxxXxxxxxxxxxXxxxxxxxxxXxxxxxxxxxXxxxxxxxxxXxxxxxxxxx
	------------------------------------------------------------------------------------------------*/
	cabec1:=If(mv_par01==2,oemtoansi(STR0070),Cabec1)
	dbSelectArea("TRB")
	dbGoTop()
	SetRegua(LastRec())
	While !Eof()

		#IFNDEF WINDOWS
			If LastKey() = 286    //ALT_A
				lEnd := .t.
			EndIf
		#ENDIF

		If lEnd
			@PROW()+1,001 PSAY oemtoansi(STR0071)
			Exit
		Endif

		IncRegua()
		//Ŀ
		// Filtra as solicitacoes maior que o numero definido 	       
		//
		If Li > 60
			cabec(titulo,cabec1,cabec2,nomeprog,tamanho,15)
			Li++
			@ Li,0 PSAY xFiltro1
			Li++
			@ Li,0 PSAY xFiltro2
			Li++
		Endif
		//Ŀ
		// Adiciona 1 ao contador de registros impressos         
		//
		nCntImpr++
		If (mv_par01==1 .OR. mv_par01==3)
			Li++
			@ Li,02 PSAY TIPO
			@ Li,06 PSAY EMPRESA+"/"+FILIAL
			@ Li,13 PSAY MODULO
			@ Li,24 PSAY USUARIO
			@ Li,40 PSAY PROGRAMA
			@ Li,49 PSAY HORA
			@ Li,59 PSAY ARQUIVO
			@ Li,65 PSAY REGISTRO
			@ Li,76 PSAY PORTA
		EndIf
		If (mv_par01==2 .or. mv_par01==3)
			Li++
			@ Li,00 PSAY oEmToAnsi(RESUMO)
		Endif

		If Li > 60
			Li:=66
		Endif

		dbSkip()

	EndDO

	If li != 80
		Roda(nCntImpr,cRodaTxt,Tamanho)
	EndIf

	Set Device to Screen

	If aReturn[5] = 1
		Set Printer To
		dbCommitAll()
		OurSpool(wnrel)
	Endif

	MS_FLUSH()

	Return

