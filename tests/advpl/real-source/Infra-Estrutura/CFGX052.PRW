#INCLUDE "PROTHEUS.CH"
#INCLUDE "CFGX052.CH"
#INCLUDE "DBTREE.CH"

#DEFINE DF_VISUAL     2
#DEFINE DF_INCLUIR    3
#DEFINE DF_ALTERAR    4
#DEFINE DF_EXCLUIR    5

Static aList := {}     //Cache da pesquisa de campos

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณCFGX052   บ Autor ณ Fernando Salvatori บ Data ณ 21/01/2004  บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Configuracao de LOG de alteracao nos cadatros.             บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ GENERICO                                                   บฑฑ
ฑฑฬออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบ           ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.           บฑฑ
ฑฑฬออออออออออออออัออออออออัออออออัออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบ PROGRAMADOR  ณ DATA   ณ BOPS ณ  MOTIVO DA ALTERACAO                   บฑฑ
ฑฑฬออออออออออออออุออออออออุออออออุออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบ              ณ        ณ      ณ                                        บฑฑ
ฑฑศออออออออออออออฯออออออออฯออออออฯออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function CFGX052
	Local oExplorer                     //Objeto Explorer
	Local oListBox                      //ListBox do explores
	Local aResource   := {}             //Painel do Resource
	Local nI          := 0              //Contador do sistema
	Local nTop        := 01             //Posicao acima da dialog
	Local nLeft       := 01             //Posicao da esquerda do dialog
	Local nBottom	  := 0              //posicao abaixo do dialog
	Local nRight	  := 0              //posicao da direita do dialog
	Local oPanels     := {}             //variavel contendo os objetos paineis em uma array
	Local nPanel      := 0              //Qtde de paineis na array

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Abertura do arquivo SXO                                                ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	dbSelectArea( "SXO" )
	dbSetOrder( 1 )
	dbGotop()

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Objeto explorer.                                                       ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	oExplorer := MSExplorer():New( STR0001 )//"Configuracao de LOG de campos"
	oExplorer:DefaultBar()

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณCriacao da Arvore          - Nivel 0 ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	Aadd( oPanels, oExplorer:AddTree(STR0002,"BMPPARAM",,PadR("CFG052Man",20)) )//"Campos para LOG"
	nPanel := Len( oPanels )

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณCalculo do Explorer para varios pixeis do windows                       ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	nBottom	  := Int((oExplorer:aPanel[nPanel]:nHeight * .99))
	nRight	  := Int((oExplorer:aPanel[nPanel]:nWidth * .49))

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Objeto ListBox aberto diretamente a base de dados SXO                  ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	@ nTop, nLeft LISTBOX oListBox FIELDS SXO->XO_ALIAS,;
		SXO->XO_CAMPO,;
		SXO->XO_OPER ;
		HEADER STR0003,;//"Alias"
	STR0004,;//"Campo"
	STR0005 ;//"Ordem"
	ALIAS  "SXO" ;
		SIZE   nRight,(nBottom * 0.5) ;
		OF     oExplorer:GetPanel(nPanel) PIXEL

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณMonta o conjunto de botoes da tool bar do objeto Explorer e as respectivas chamadas das funcoesณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	oExplorer:AddSearchButton({|cSearch,lSCase,lSWord| CFG052Pesq(cSearch,lSCase,lSWord)})
	AAdd( aResource, {"FINAL"     , STR0006 , {|| oExplorer:Deactivate()}})//"Sair"
	AAdd( aResource, {"BMPINCLUIR", STR0007 , {|| CFG052Man("SXO",SXO->(Recno()),DF_INCLUIR,oExplorer,oListBox)} })//"Incluir"
	AAdd( aResource, {"BMPVISUAL" , STR0008 , {|| CFG052Man("SXO",SXO->(Recno()),DF_VISUAL,oExplorer,oListBox)}  })//"Visualizar"
	AAdd( aResource, {"EDIT"      , STR0009 , {|| CFG052Man("SXO",SXO->(Recno()),DF_ALTERAR,oExplorer,oListBox)} })//"Alterar"
	AAdd( aResource, {"EXCLUIR"   , STR0010 , {|| CFG052Man("SXO",SXO->(Recno()),DF_EXCLUIR,oExplorer,oListBox)} })//"Excluir"

	For nI := 1 To Len(aResource)
		oExplorer:AddDefButton( aResource[nI,1], aResource[nI,2], aResource[nI,3] )
	Next nI

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Ativa objeto explorer.                                                 ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	oExplorer:Activate(.T.,{|| .T. })

	Return

	/*
	
	ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
	ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
	ฑฑบPrograma  ณCFG052Man บAutor  ณFernando Salvatori  บ Data ณ 21/01/2004  บฑฑ
	ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
	ฑฑบDesc.     ณ Manutencao das informacoes                                 บฑฑ
	ฑฑบ          ณ                                                            บฑฑ
	ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
	ฑฑบUso       ณ CFGX052                                                    บฑฑ
	ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
	ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
	฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
	*/
Function CFG052Man(cAlias, nRecno, nOpc, oExplorer, oListBox)
	Local oDlg                                 //Dialog principal
	Local oPanel                               //Panel Principal
	Local aSize     := MsAdvSize()             //Size da tela.
	Local aObjects  := {}                      //Objetos da tela
	Local aPosObj   := {}                      //Posicoes do objeto
	Local aInfo     := {}                      //Posicoes do objeto
	Local aCriaCols := {}                      //Array que recebe o retorno da funcao a610criacols
	Local oTabela                              //Objeto da Tabela (GET)
	Local aButtons  := {}                      //Array com os botoes para enchoicebar
	Local lOk       := .F.                     //Confirmacao da atualizacao dos dados
	Local aCols     := {}                      //Colunas do msgetdados

	Private cTabela := Space(3)                //Tabela do sistema (GET)
	Private aHeader := {}                      //Header do msgetdados
	Private aColsR  := {}                      //Recno dos dados da CriaCols
	Private oGetDados                          //Objeto da MsGetDados

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Adiciona o botao de Estrutura do arquivo.                               ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	AAdd(aButtons,{"NOVACELULA"  ,{|| IIf(Str(nOpc,1)$"2/5",Nil,CFG052AtuACols(aClone( CFG052List(cTabela)) ))},STR0011} )//"Consultar Estrutura..."
	AAdd(aButtons,{"BMPCONS"     ,{|| IIf(Str(nOpc,1)$"2/5",Nil,GdSeek(oGetDados)) },STR0012} )//"Pesquisar..."
	AAdd(aButtons,{"LBOK"        ,{|| IIf(Str(nOpc,1)$"2/5",Nil,CFG052TpLog()) },STR0013} )//"Selecionar tipo de LOG..."

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Carrega variaveis de memoria.                                           ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	dbSelectArea( "SXO" )
	RegToMemory( "SXO", ( nOpc == 3 ) )

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Montagem do Header.                                                     ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	AAdd(aHeader,{"Campo"   ,"XO_CAMPO","@!",010,0 ,"CFG052VldCpo()" ,"๛" ,"C"," "," ",NIL,,,,})
	AAdd(aHeader,{"Operacao","XO_OPER" ,"999",003,0 ,".T." ,"๛" ,"N"," "," ",NIL,,,"V",})

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Monta aCols do sistema.                                                 ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	dbSelectArea( "SXO" )
	dbSetOrder( 1 )
	If nOpc == 3
		aCriaCols := aClone( CFG052Cria("SXO", aHeader, "   ", {|| .F.}) )
	Else
		cTabela := SXO->XO_ALIAS
		aCriaCols := aClone( CFG052Cria("SXO", aHeader, SXO->XO_ALIAS,{|| SXO->XO_ALIAS == cTabela }) )
	EndIf

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Faco a distribuicao dos dados de retorno da CFG052Cria                  ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	aCols  := aClone( aCriaCols[1] )
	aColsR := aClone( aCriaCols[2] )

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Define as posicoes da GetDados e Paineis.                               ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	AAdd( aObjects, {  100, 020, .T., .F., .T. } ) //Painel Cabecalho
	AAdd( aObjects, {  100, 100, .T., .T. } )      //MsGetDados
	aInfo 	 := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 2, 2 }
	aPosObj  := MsObjSize( aInfo, aObjects, .T. , .F. )

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Montagem da tela.                                                       ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	DEFINE MSDIALOG oDlg TITLE STR0014 From aSize[7],00 To aSize[6],aSize[5] OF oMainWnd PIXEL//"Configuracao de LOG de campos"

	oPanel := TPanel():New(aPosObj[1,1],aPosObj[1,2],"",oDlg,NIL,.T.,.F.,NIL,NIL,aPosObj[1,3],aPosObj[1,4],.T.,.F. )

	@ 05,010 SAY STR0015              SIZE 30,9  OF oPanel PIXEL//"Tabela"
	@ 05,044 GET oTabela VAR cTabela  SIZE 25,9 PICTURE "@!" OF oPanel VALID CFG052VlGet(oTabela,cTabela) WHEN Empty(cTabela) PIXEL

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ MsGetDados.                                                             ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	oGetDados := MsNewGetDados():New(aPosObj[2][1],aPosObj[2][2],aPosObj[2][3],aPosObj[2][4],;
		IIf( nOpc == 2 .Or. nOpc == 5,0,GD_INSERT+GD_DELETE+GD_UPDATE),"CFG052LinOk",,"",,,120,,,,oDlg,aHeader,aCols)

	oGetDados:lNewLine := .F.

	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| IIf(lOK := CFG052tudOk(nOpc),oDlg:End(),NIL)},{||oDlg:End()},,aButtons)

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Confirmacao para gravacao dos dados.                                    ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If lOk .And. nOpc != 2
		CFG052Grava(nOpc,cTabela)
	EndIf

	Return .T.

	/*
	
	ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
	ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
	ฑฑบPrograma  ณCFG052LinOบAutor  ณFernando Salvatori  บ Data ณ 21/01/2004  บฑฑ
	ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
	ฑฑบDesc.     ณ Verifica se todos os dados estao ok.                       บฑฑ
	ฑฑบ          ณ                                                            บฑฑ
	ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
	ฑฑบUso       ณ CFGX052                                                    บฑฑ
	ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
	ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
	฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
	*/
Function CFG052LinOk()

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ1 - Apenas valida                                                                   ณ
	//ณ2 - Valida e exibe mensagem de erro                                                 ณ
	//ณ3 - Valida e exibe mensagem de erro informando os campos                            ณ
	//ณ4 - Valida e exibe mensagem de erro informando os campos e as linhas com duplicidadeณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Verifico se existe duplicidade das linhas do aCols                      ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If !gdDeleted( N ) .And. !gdCheckKey( {"XO_CAMPO"}, 4 )
		Return .F.
	EndIf

	Return .T.

	/*
	
	ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
	ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
	ฑฑบPrograma  ณCFG052TudOบAutor  ณFernando Salvatori  บ Data ณ 21/01/2004  บฑฑ
	ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
	ฑฑบDesc.     ณ Verifica se todos os dados estao ok.                       บฑฑ
	ฑฑบ          ณ                                                            บฑฑ
	ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
	ฑฑบUso       ณ CFGX052                                                    บฑฑ
	ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
	ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
	฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
	*/
Static Function CFG052tudOk(nOpc)
	Local nX          := 0                     //Contador do Sistema
	Local nY          := 0                     //Contador do Sistema
	Local lDeletados  := .T.                   //Variavel que confere se todos os registros estao deletados
	Local nLenCols    := Len( oGetDados:aCols )//Len do aCols
	Local nLenHead    := Len( aHeader )        //Len do aHeader
	Private aCols     := oGetDados:aCols       //aCols para validar o gdcheckkey
	Private N         := 0                     //aCols para validar o gdcheckkey

	If nOpc == 2 .Or. nOpc == 5
		Return .T.
	EndIf

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Verifico as inconsistencias de todas as linhas.                         ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	For nX := 1 to nLenCols

		N := nX //Atualizacao do N para a funcao gdCheckKey

		If !gdDeleted( nX,,oGetDados:aCols )

			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณ Se existe registros validos, atualizo o conteudo de lDeletados          ณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
			If lDeletados
				lDeletados := .F.
			EndIf

			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณ Checo os campos obrigatorios do sistema.                                ณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
			For nY := 1 to nLenHead
				If Empty( oGetDados:aCols[nX][nY] )
					MsgStop( STR0016  , STR0017 )//"Existem campos obrigat๓rios em branco, verifique !"
					oGetDados:nAt := nY
					oGetDados:oBrowse:Refresh()
					Return .F.
				EndIf
			Next nY

			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณ Verifico se existe duplicidade das linhas do aCols                      ณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณ1 - Apenas valida                                                                   ณ
			//ณ2 - Valida e exibe mensagem de erro                                                 ณ
			//ณ3 - Valida e exibe mensagem de erro informando os campos                            ณ
			//ณ4 - Valida e exibe mensagem de erro informando os campos e as linhas com duplicidadeณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
			If !gdCheckKey( {"XO_CAMPO"}, 4 )
				Return .F.
			EndIf
		EndIf
	Next nX

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Permitir seguir adiante caso exista pelo menos 1 linha valida.          ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If lDeletados
		MsgStop( STR0018 , STR0017 )//"Todos os registros estใo deletados, favor selecionar um registro vแlido para grava็ใo."//"Aten็ใo"
		Return .F.
	EndIf

	Return .T.


	/*
	
	ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
	ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
	ฑฑบPrograma  ณCFG052VlGeบAutor  ณFernando Salvatori  บ Data ณ 21/01/2004  บฑฑ
	ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
	ฑฑบDesc.     ณ Validacao do GET.                                          บฑฑ
	ฑฑบ          ณ                                                            บฑฑ
	ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
	ฑฑบUso       ณ CFGX052                                                    บฑฑ
	ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
	ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
	฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
	*/
Static Function CFG052VlGet(oTabela,cTabela)

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Checo se o usuario digitou uma tabela valida.                           ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If Empty( cTabela )
		MsgStop( STR0019 , STR0017 )//"Selecione uma tabela para prosseguir."//"Aten็ใo"
		Return .F.
	EndIf

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Checo se a tabela existe no dicionario de arquivos (SX2)                ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	SX2->( dbSetOrder( 1 ) )
	If ! SX2->( MsSeek( cTabela ) )
		MsgStop( STR0020 , STR0017 )//"Selecione uma tabela vแlida."//"Aten็ใo"
		Return .F.
	EndIf

	If Empty(SX2->X2_UNICO)
		MsgStop(STR0038,STR0017)	//"Tabela sem chave unica"//"Aten็ใo"
		Return .F.
	EndIf

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Checo se existe a tabela digitada jah cadastrada no sistema.            ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	dbSelectArea( "SXO" )
	dbSetOrder( 1 )
	If dbSeek( cTabela )
		MsgStop( STR0021 , STR0017 )//"Jแ existe registros relacionados a tabela selecionada."//"Aten็ใo"
		Return .F.
	EndIf

	oTabela:Disable()

	Return .T.

	/*
	
	ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
	ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
	ฑฑบPrograma  ณCFG052GravบAutor  ณFernando Salvatori  บ Data ณ 21/01/2004  บฑฑ
	ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
	ฑฑบDesc.     ณ Efetua a grava็ใo dos campos.                              บฑฑ
	ฑฑบ          ณ                                                            บฑฑ
	ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
	ฑฑบUso       ณ CFGX052                                                    บฑฑ
	ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
	ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
	฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
	*/
Static Function CFG052Grava(nOpc,cTabela)

	Begin Transaction

		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Para inclusao e alteracao, faco a atualizacao do arquivo.               ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If nOpc == 3 .Or. nOpc == 4
			//Grava os dados de configuracao
			dbSelectArea("SXO")
			dbSetOrder( 1 )
			CFG052GrvCols(oGetDados:aCols, aHeader, aColsR, "SXO",{|| SXO->XO_ALIAS := cTabela} )

			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณ Para exclusao dos dados.                                                ณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		ElseIf nOpc == 5
			// Exclui o cadastro de clientes padrao
			dbSelectArea("SXO")
			dbSetOrder( 1 )
			dbSeek( cTabela )

			Do While !Eof() .And. XO_ALIAS == cTabela
				RecLock("SXO", .F., .T.)
				dbDelete()
				MsUnlock()
				dbSkip()
			EndDo
		EndIf

	End Transaction

	Return

	/*
	
	ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
	ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
	ฑฑบPrograma  ณCFG052VldCบAutor  ณFernando Salvatori  บ Data ณ 21/01/2004  บฑฑ
	ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
	ฑฑบDesc.     ณ Faz a validacao do campos.                                 บฑฑ
	ฑฑบ          ณ                                                            บฑฑ
	ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
	ฑฑบUso       ณ CFGX052                                                    บฑฑ
	ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
	ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
	฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
	*/
Function CFG052VldCpo()
	Local cCampo  := &( ReadVar() )                   //Valor do campo do sistema
	Local nPos    := N                                //Posicao de N
	Local nPosCampo := gdFieldGet( "XO_CAMPO" )      //Posicao do campo XO_CAMPO
	Local nX      := 0                                //Contador do Sistema

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Executo a validacao para os campos que nao sao deletados;               ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If !gdDeleted( nPos )

		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Verifico se o campo esta no dicionario de dados do sistema (SX3)        ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		SX3->( dbSetOrder( 2 ) )
		If ! SX3->( MsSeek( cCampo ) )
			Help( " ",1,"REGNOIS")
			Return .F.
		EndIf

		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Verifico se o campo digitado pertence a tabela selecionada.             ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If SX3->X3_ARQUIVO != cTabela
			MsgStop( STR0022 ,STR0017 )//"Selecione apenas os campos da tabela selecionada."//"Aten็ใo"
			Return .F.
		EndIf
	EndIf

	Return .T.


	/*
	
	ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
	ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
	ฑฑบPrograma  ณCFG052AtuAบAutor  ณFernando Salvatori  บ Data ณ 21/01/2004  บฑฑ
	ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
	ฑฑบDesc.     ณ Atualiza aCols com os campos selecionados na selecao de    บฑฑ
	ฑฑบ          ณ campos                                                     บฑฑ
	ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
	ฑฑบUso       ณ CFGX052                                                    บฑฑ
	ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
	ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
	฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
	*/
Function CFG052AtuACols(aCampos)
	Local nX         := 0    //Loop em aCampos
	Local nY         := 0    //Loop em aHeader
	Local nPos       := 0    //Posicao do campo dentro do aCols
	Local nPosCpo    := gdFieldPos( "XO_CAMPO" )
	Local nPosOper   := gdFieldPos( "XO_OPER" )
	Local nLenHead   := Len( aHeader )
	Local nPosPesq   := 0    //Pesquisa do campo aCols
	Local nPosGrava  := 0    //Posicao para gravar registro dentro do aCols

	If Empty( aCampos )
		Return
	EndIf

	For nX := 1 to Len( aCampos )
		nPosPesq := aScan( oGetDados:aCols,{| ExpA1 | PadR(ExpA1[nPosCpo],10) == PadR(aCampos[nX][1],10) })
		If nPosPesq <= 0 .Or. aTail( oGetDados:aCols[nPosPesq] )

			If nPosPesq <= 0
				nPosGrava := aScan( oGetDados:aCols,{| ExpA1 | PadR(ExpA1[nPosCpo],10) == Space(10) })

				If nPosGrava <= 0
					AAdd( oGetDados:aCols, Array( nLenHead + 1 ) )
					nPosGrava := Len( oGetDados:aCols )
				EndIf
			Else
				nPosGrava := nPosPesq
			EndIf
			oGetDados:aCols[nPosGrava][nPosCpo]    := aCampos[nX][1]
			oGetDados:aCols[nPosGrava][nPosOper]   := 0
			oGetDados:aCols[nPosGrava][nPosOper]   += IIf( aCampos[nX][2],64,0 )
			oGetDados:aCols[nPosGrava][nPosOper]   += IIf( aCampos[nX][3],128,0 )
			oGetDados:aCols[nPosGrava][nPosOper]   += IIf( aCampos[nX][4],256,0 )
			oGetDados:aCols[nPosGrava][nLenHead+1] := .F.
		EndIf
	Next nX

	oGetDados:Refresh()

	Return

	/*
	
	ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
	ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
	ฑฑบPrograma  ณCFG052CriaบAutor  ณFernando Salvatori  บ Data ณ 21/01/2004  บฑฑ
	ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
	ฑฑบDesc.     ณ Cria aCols para MSNEWGETDADOS                              บฑฑ
	ฑฑบ          ณ                                                            บฑฑ
	ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
	ฑฑบUso       ณ CFGX052                                                    บฑฑ
	ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
	ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
	฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
	*/
Function CFG052Cria(cAlias, aHeaLoc, cSeek, bWhil, bFor)
	Local aRetCol := {}           //Retorno do aCols
	Local aRetReg := {}           //Retorno dos recnos
	Local x       := 0            //Contador
	Default cAlias := Alias()
	Default bWhil  := {|| .T.}
	Default bFor   := {|| .T.}

	dbSelectArea(cAlias)
	dbSeek(cSeek, .T.)

	Do While ! Eof() .And. Eval(bWhil)
		If ! Eval(bFor)
			dbSkip()
			Loop
		Endif

		AAdd(aRetCol, {})

		For X := 1 to Len(aHeaLoc)
			Aadd(aRetCol[Len(aRetCol)], FieldGet(FieldPos(aHeaLoc[x, 2])))
		Next
		Aadd(aRetCol[Len(aRetCol)], .F.)
		Aadd(aRetReg, Recno())
		dbSkip()
	Enddo

	Return({aRetCol, aRetReg})

	/*
	
	ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
	ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
	ฑฑบPrograma  ณCFG052CriaบAutor  ณFernando Salvatori  บ Data ณ 21/01/2004  บฑฑ
	ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
	ฑฑบDesc.     ณ Grava aCols para MSNEWGETDADOS                             บฑฑ
	ฑฑบ          ณ                                                            บฑฑ
	ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
	ฑฑบUso       ณ CFGX052                                                    บฑฑ
	ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
	ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
	฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
	*/
Function CFG052GrvCols(aCols, aHeader, aRegCol, cAlias, bBloco, cCampos)
	Local i
	Local j
	Local z
	Local lEmpty  := .F.
	Default cAlias := Alias()

	dbSelectArea(cAlias)

	For i := 1 to Len(aCols)
		If i <= Len(aRegCol)
			dbGoto(aRegCol[i])
			RecLock(cAlias, .f.)
			If aCols[i, Len(aHeader)+1]
				dbDelete()
				MsUnlock()
				Loop
			Endif
		Else
			If ValType(cCampos) == "C"
				For j := 1 to Len(aHeader)
					If AllTrim(aHeader[j, 2]) $ Upper(cCampos)
						If Empty(aCols[i, j])
							lEmpty := .T.
							Loop
						Endif
					Endif
				Next
			Endif
			If ! aCols[i, Len(aHeader)+1] .And. ! lEmpty
				RecLock(cAlias, .T.)
			Else
				Loop
			Endif
		Endif
		For z := 1 to Len(aHeader)
			If (nFieldPos := FieldPos(aHeader[z, 2])) > 0
				FieldPut(nFieldPos, aCols[i, z])
			Endif
		Next

		Eval(bBloco)
		MsUnlock()
	Next

	Return(.T.)


	/*
	
	ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
	ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
	ฑฑบFuncao    ณCFG052ListบAutor  ณFernando Salvatori  บ Data ณ 21/01/2004  บฑฑ
	ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
	ฑฑบDesc.     ณ Funcao para selecionar campos para retorno.                บฑฑ
	ฑฑบ          ณ                                                            บฑฑ
	ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
	ฑฑบUso       ณ Generico.                                                  บฑฑ
	ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
	ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
	฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
	*/
Function CFG052List(cTabela,aCampos)
	Local nX := 0                                     //Contador do sistema
	Local oDlgCons                                    //Dialog da Listbox
	Local oListBox                                    //Objeto da listbox
	Local aListCpos := {}                             //Array da ListBox
	Local nPosAlias := 0                              //Posicao do campo de Alias
	Local oOk := LoadBitmap(GetResources(), "LBOK")   //Desenho do Checked
	Local oNo := LoadBitmap(GetResources(), "LBNO")   //Desenho do Not Checked
	Local oCheckBox                                   //Objeto CheckBos
	Local lCheckBox := .F.                            //Selecionar todos os campos do ListBox
	Local aRet := {}                                  //Retorno da funcao
	Local lOk  := .F.                                 //Confirmacao da dialog
	Local oCheckInc                                   //Objeto CheckBos
	Local lCheckInc := .F.                            //Selecionar todos os campos do ListBox
	Local oCheckAlt                                   //Objeto CheckBos
	Local lCheckAlt := .F.                            //Selecionar todos os campos do ListBox
	Local oCheckExc                                   //Objeto CheckBos
	Local lCheckExc := .F.                            //Selecionar todos os campos do ListBox

	Default cTabela := ""
	Default aCampos := {}

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Nao faco a montagem da estrutura para o caso da tabela estiver em brancoณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If Empty( cTabela )
		Return aRet
	EndIf

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Caso a Array estatica aList esteja em branco, faz o preenchimento, esta ณ
	//ณ Array tem por objetivo, fazer o <Cache> dos campos da tabela MA6.       ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If Empty( aList )
		dbSelectArea( "SX3" )
		dbSetOrder( 1 )
		MsSeek( cTabela )

		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Faz a montagem da estrutura da tabela selecionada                       ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		Do While !Eof() .And. X3_ARQUIVO == cTabela
			If X3_CONTEXT <> "V"
				AAdd( aListCpos,{.F.,X3_TITULO,X3_CAMPO,X3_TAMANHO,X3_DECIMAL} )
				If aScan( aCampos,{| ExpA1 | PadR(ExpA1,10) == aListCpos[Len(aListCpos)][3] } ) > 0
					aListCpos[Len(aListCpos)][1] := .T.
				Else
					aListCpos[Len(aListCpos)][1] := .F.
				EndIf
			EndIf

			dbSkip()
		EndDo

		If Empty( aListCpos )
			MsgStop( STR0023 , STR0017 )//"Nใo existe campos em uso da tabela selecionada!"//"Aten็ใo"
			Return aRet
		EndIf

		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Guardo a estrutura em Cache.                                            ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		AAdd( aList, {cTabela,aListCpos})
	Else

		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Se a estrutura existe em cache, retiro a mesma, caso contrario, monto e ณ
		//ณ depois guardo em cache.                                                 ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		nPosAlias := aScan( aList,{|ExpA1| ExpA1[1] == cTabela})
		If nPosAlias > 0
			aListCpos := aClone( aList[nPosAlias][2] )

			For nX := 1 to Len( aListCpos )
				If aScan( aCampos,{| ExpA1 | PadR(ExpA1,10) == aListCpos[nX][3] } ) > 0
					aListCpos[nX][1] := .T.
				Else
					aListCpos[nX][1] := .F.
				EndIf
			Next nX

		Else
			dbSelectArea( "SX3" )
			dbSetOrder( 1 )
			MsSeek( cTabela )

			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณ Faz a montagem da estrutura do MA6                                      ณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
			Do While !Eof() .And. X3_ARQUIVO == cTabela
				If X3USO( X3_USADO )
					AAdd( aListCpos,{.F.,X3_TITULO,X3_CAMPO,X3_TAMANHO,X3_DECIMAL} )
					If aScan( aCampos,{| ExpA1 | PadR(ExpA1,10) == aListCpos[Len(aListCpos)][3] } ) > 0
						aListCpos[Len(aListCpos)][1] := .T.
					Else
						aListCpos[Len(aListCpos)][1] := .F.
					EndIf
				EndIf

				dbSkip()
			EndDo

			If Empty( aListCpos )
				MsgStop( STR0023 , STR0017 )//"Nใo existe campos em uso da tabela selecionada!"//"Aten็ใo"
				Return aRet
			EndIf

			AAdd( aList, {cTabela,aListCpos})
		EndIf
	EndIf

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Montagem da Tela.                                                       ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	DEFINE MSDIALOG oDlgCons TITLE STR0024 FROM 9,0 TO 36,52 OF oMainWnd//"Estrutura de arquivo"

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Listbox.                                                                ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	@ .5,.7 LISTBOX oListBox VAR cListBox Fields HEADER "",STR0025,STR0026,STR0027,STR0028 SIZE 155,145//"Nome"//"Tํtulo"//"Tamanho"//"Decimais"

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Faz a configuracao da ListBox.                                          ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	oListBox:SetArray(aListCpos)
	oListBox:bLine      := { || { IIf(aListCpos[oListBox:nAt][1],oOk,oNo),aListCpos[oListBox:nAt,2],aListCpos[oListBox:nAt,3],aListCpos[oListBox:nAt,4],aListCpos[oListBox:nAt,5]} }
	oListBox:bLDblClick := {|| aListCpos[oListBox:nAt][1] := !aListCpos[oListBox:nAt][1],oListBox:Refresh(),lCheckBox := .F.,oCheckBox:Refresh() }

	@ 160,6 CHECKBOX oCheckBox VAR lCheckBox PROMPT STR0028 SIZE 60,7 OF oDlgCons ON CHANGE ;//"Inverter Todos"
	( aEval(aListCpos,{ |ExpA1| ExpA1[1] := !ExpA1[1] }),oListBox:Refresh() )

	@ 170,6 TO 200,160 LABEL STR0029 PIXEL//"Tipo de LOG"

	@ 183,25  CHECKBOX oCheckInc VAR lCheckInc PROMPT STR0030  SIZE 40,7 OF oDlgCons//"Inclusใo"
	@ 183,065 CHECKBOX oCheckAlt VAR lCheckAlt PROMPT STR0031  SIZE 40,7 OF oDlgCons//"Altera็ใo"
	@ 183,105 CHECKBOX oCheckExc VAR lCheckExc PROMPT STR0032  SIZE 40,7 OF oDlgCons//"Exclusใo"

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Botao de saida.                                                         ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	DEFINE SBUTTON FROM 004,170 TYPE 1 ACTION (lOk := .T., oDlgCons:End()) ENABLE OF oDlgCons
	DEFINE SBUTTON FROM 020,170 TYPE 2 ACTION (lOk := .F., oDlgCons:End()) ENABLE OF oDlgCons

	ACTIVATE MSDIALOG oDlgCons CENTERED

	If lOk
		For nX := 1 to Len( aListCpos )
			If aListCpos[nX][1]
				AAdd( aRet, {aListCpos[nX][3],lCheckInc,lCheckAlt,lCheckExc} )
			EndIf
		Next nX
	EndIf

	Return aRet


	/*
	
	ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
	ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
	ฑฑบPrograma  ณCFG052TpLoบAutor  ณFernando Salvatori  บ Data ณ 21/01/2004  บฑฑ
	ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
	ฑฑบDesc.     ณ Tipo de LOG do cadastro.                                   บฑฑ
	ฑฑบ          ณ                                                            บฑฑ
	ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
	ฑฑบUso       ณ CFGX052                                                    บฑฑ
	ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
	ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
	฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
	*/
Function CFG052TpLog()
	Local nPos     := oGetDados:nAt                //Posicao atual do registro do aCols
	Local nPosOper := gdFieldPos( "XO_OPER" )      //Posicao do campo XO_OPER
	Local lOk  := .F.                              //Retorno do Dialog
	Local oCheckInc                                //Objeto "Inclusao" de Check Box
	Local lCheckInc := .F.                         //Variavel com conteudo do check box
	Local oCheckAlt                                //Objeto "Alteracao" de Check Box
	Local lCheckAlt := .F.                         //Variavel com conteudo do check box
	Local oCheckExc                                //Objeto "Exclusao" de Check Box
	Local lCheckExc := .F.                         //Variavel com conteudo do check box
	Local oCheckPar                                //Objeto "parcial" para check box
	Local lCheckPar := .F.                         //Conteudo do Check box
	Local oCheckTot                                //Objeto "total" para check box
	Local lCheckTot := .F.                         //Variavel com conteudo para check box total
	Local oDlg                                     //Dialogo principal
	Local oGroup                                   //Grupo do Radio
	Local oRadio                                   //Objeto doRadio buttom
	Local nRadio                                   //Variavel com conteudo do Radio
	Local nX        := 0                           //Contador do sistema


	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Montagem da tela de tipo de LOG.                                        ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	DEFINE MSDIALOG oDlg TITLE STR0029 FROM 9,0 TO 23,42 OF oMainWnd//"Tipo de LOG"

	@ 03,03 TO 35,160 LABEL STR0033 PIXEL//"Selecione o tํpo de opera็ใo:"

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Check Box para os tipos de operacao                                     ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	@ 15,25  CHECKBOX oCheckInc VAR lCheckInc PROMPT STR0030  SIZE 40,7 OF oDlg//"Inclusใo"
	@ 15,065 CHECKBOX oCheckAlt VAR lCheckAlt PROMPT STR0031  SIZE 40,7 OF oDlg//"Altera็ใo"
	@ 15,105 CHECKBOX oCheckExc VAR lCheckExc PROMPT STR0032  SIZE 40,7 OF oDlg//"Exclusใo"

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Configuracoes de Radio para o tipo de atualizacao do aCols              ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	@ 39,03 GROUP oGroup TO 80,160 LABEL STR0034 OF oDlg PIXEL//"Tipo de Atualiza็ใo"
	@ 46,19 RADIO oRadio VAR nRadio 3D SIZE 130,10 PROMPT STR0035, ;//"Atualizar o campo selecionado"
	STR0036, ;//"Atualizar somente campos nใo preenchidos"
	STR0037 OF oDlg PIXEL//"Atualizar todos os campos"

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Botoes de Saida/Confirmacao                                             ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	DEFINE SBUTTON FROM 86,100 TYPE 1 ACTION (lOk := .T., oDlg:End()) ENABLE OF oDlg
	DEFINE SBUTTON FROM 86,134 TYPE 2 ACTION (lOk := .F., oDlg:End()) ENABLE OF oDlg

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Ativando Dialogo.                                                       ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	ACTIVATE MSDIALOG oDlg CENTERED

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Caso seja confirmado                                                    ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If lOk

		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Atualizacao apenas no registro selecionado.                             ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If nRadio == 1
			oGetDados:aCols[nPos][nPosOper] := 0
			oGetDados:aCols[nPos][nPosOper] += IIf( lCheckInc,064,0 )
			oGetDados:aCols[nPos][nPosOper] += IIf( lCheckAlt,128,0 )
			oGetDados:aCols[nPos][nPosOper] += IIf( lCheckExc,256,0 )

			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณ Atualizacao parcial (somente operacoes nao definidas) ou total.         ณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		ElseIf nRadio == 2 .Or. nRadio == 3
			For nX := 1 to Len( oGetDados:aCols )
				If nRadio == 2 .And. oGetDados:aCols[nX][nPosOper] != 0
					Loop
				EndIf
				oGetDados:aCols[nX][nPosOper] := 0
				oGetDados:aCols[nX][nPosOper] += IIf( lCheckInc,064,0 )
				oGetDados:aCols[nX][nPosOper] += IIf( lCheckAlt,128,0 )
				oGetDados:aCols[nX][nPosOper] += IIf( lCheckExc,256,0 )
			Next nX
		EndIf

		oGetDados:Refresh()
	EndIf

	Return Nil

	/*
	
	ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
	ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
	ฑฑบPrograma  ณCFG052PesqบAutor  ณFernando Salvatori  บ Data ณ 21/01/2004  บฑฑ
	ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
	ฑฑบDesc.     ณ Funcao para pesquisa de dados.                             บฑฑ
	ฑฑบ          ณ                                                            บฑฑ
	ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
	ฑฑบUso       ณ CFGX052                                                    บฑฑ
	ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
	ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
	฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
	*/
Static Function CFG052Pesq(cSearch,lSCase,lSWord)
	Local cTabela := ""                   //Tabela utilizada pelo Seek

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Caso o usuario deseja coicidir maiusculas/minusculas.                   ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If lSCase
		cSearch := Upper( cSearch )
	EndIf

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Caso o usuario deseja procurar palavras inteiras.                       ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If lSWord
		cSearch := PadR( cSearch , 13 )
	Else
		cSearch := Alltrim(cSearch)
	EndIf

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Identificacao da tabela para pesquisa.                                  ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If SubStr(cSearch,3,1) == "_"
		cTabela := "S" + Upper( SubStr( cSearch,1,2) )
	Else
		cTabela := Upper( SubStr( cSearch,1,3) )
	EndIf

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Posicionamento do registro na base de dados.                            ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	SXO->( dbSeek( cTabela + cSearch, (!lSWord) ) )

	Return .T.